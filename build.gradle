def versionNumber() {
    def gitCount = "git rev-list HEAD --count".execute().text.trim()

    if (gitCount != "") {
        gitCount.toInteger()
    } else {
        0
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode versionNumber()
        versionName "1.1.0.${versionNumber()}"
        applicationId "com.seadowg.cluck"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        resources {
            excludes += ['LICENSE.txt']
        }
    }


    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    namespace 'com.seadowg.cluck'
}

dependencies {
    repositories {
        google()
        mavenCentral()
    }

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
